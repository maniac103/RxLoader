apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 3
        versionName "0.4.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = 'com.philosophicalhacker.rxloader'
version = android.defaultConfig.versionName

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        rxloader(MavenPublication) {
            groupId 'com.philosophicalhacker.rxloader'
            artifactId 'rxloader'
            version android.defaultConfig.versionName
            artifact file("$buildDir/outputs/aar/lib-release.aar")
            artifact sourceJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['rxloader']
    pkg {
        repo = 'RxLoader'
        name = 'com.philosophicalhacker.rxloader:rxloader'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kmdupr33/RxLoader'
        version {
            name = android.defaultConfig.versionName
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    destinationDir = new File('docs-source/static/javadocs/')
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    testImplementation 'junit:junit:4.12'
}
